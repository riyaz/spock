apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group "$rootProject.ext.groupId"
version "$rootProject.ext.versionName"

publishing {
  publications {
    Production(MavenPublication) {
      artifact("$buildDir/outputs/aar/spock-release.aar")
      groupId
      artifactId "$rootProject.ext.artifactId"
      version this.version
      artifact androidJavadocsJar
      artifact androidSourcesJar

      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
        configurations.implementation.allDependencies.each {
          // Ensure dependencies such as fileTree are not included in the pom.
          if (it.name != 'unspecified') {
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)
          }
        }
      }
    }
  }
}

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

  if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
  classifier = 'javadoc'
  from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.sourceFiles
}

artifacts {
  archives androidSourcesJar
  archives androidJavadocsJar
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_API_KEY')
  publications = ['Production']
  configurations = ['archives']
  override = true
  pkg {
    repo = 'me.riyaz'
    name = "$rootProject.ext.artifactId"
    licenses = ['GPL-2.0']
    labels = ['android', 'java']
    dryRun = false
    publish = true
    publicDownloadNumbers = false
    vcsUrl = "$rootProject.ext.vcsUrl"

    version {
      name = "$rootProject.ext.versionName"
      desc = "$rootProject.ext.description"
      released  = new Date()
      vcsTag = this.version
      gpg {
          sign = true
          passphrase = System.getenv('GPG_PASSWORD')
      }
    }
  }
}
